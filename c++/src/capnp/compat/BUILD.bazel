load("@capnp-cpp//src/capnp:cc_capnp_library.bzl", "cc_capnp_library")

exports_files([
    "json.capnp",
    "std-iterator.h",
])

# because git contains generated artifacts (which are used to bootstrap the compiler)
# we can't have cc_capnp_library for json.capnp. Expose it as cc library and a file.
cc_library(
    name = "json",
    srcs = [
        "json.c++",
        "json.capnp.c++",
    ],
    hdrs = [
        "json.capnp.h",
        "json.h",
    ],
    copts = select({
        "@platforms//os:windows": ["/TP"],  # compile .c++ files as .cpp files
        "//conditions:default": [],
    }),
    include_prefix = "capnp/compat",
    visibility = ["//visibility:public"],
    deps = [
        "//src/capnp",
    ],
)

cc_capnp_library(
    name = "http-over-capnp_capnp",
    srcs = [
        "byte-stream.capnp",
        "http-over-capnp.capnp",
    ],
    include_prefix = "capnp/compat",
    src_prefix = "src",
    visibility = ["//visibility:public"],
)

cc_library(
    name = "http-over-capnp",
    srcs = [
        "byte-stream.c++",
        "http-over-capnp.c++",
    ],
    hdrs = [
        "byte-stream.h",
        "http-over-capnp.h",
    ],
    copts = select({
        "@platforms//os:windows": ["/TP"],  # compile .c++ files as .cpp files
        "//conditions:default": [],
    }),
    include_prefix = "capnp/compat",
    visibility = ["//visibility:public"],
    deps = [
        ":http-over-capnp_capnp",
        "//src/kj/compat:kj-http",
    ],
)

cc_library(
    name = "websocket-rpc",
    srcs = [
        "websocket-rpc.c++",
    ],
    hdrs = [
        "websocket-rpc.h",
    ],
    copts = select({
        "@platforms//os:windows": ["/TP"],  # compile .c++ files as .cpp files
        "//conditions:default": [],
    }),
    include_prefix = "capnp/compat",
    visibility = ["//visibility:public"],
    deps = [
        "//src/capnp",
        "//src/kj/compat:kj-http",
    ],
)

[cc_test(
    name = f.removesuffix(".c++"),
    srcs = [f],
    copts = select({
        "@platforms//os:windows": ["/TP"],  # compile .c++ files as .cpp files
        "//conditions:default": [],
    }),
    deps = [
        ":http-over-capnp",
        ":websocket-rpc",
        "//src/capnp:capnp-test",
    ],
) for f in [
    "byte-stream-test.c++",
    "http-over-capnp-test.c++",
    "websocket-rpc-test.c++",
]]

cc_library(
    name = "http-over-capnp-test-as-header",
    hdrs = ["http-over-capnp-test.c++"],
)

cc_test(
    name = "http-over-capnp-old-test",
    srcs = ["http-over-capnp-old-test.c++"],
    deps = [
        ":http-over-capnp",
        ":http-over-capnp-test-as-header",
        "//src/capnp:capnp-test",
    ],
)
